100 DIM A$(15):REM ALLOCATE SPACE FOR STRING MATRIX
110 FOR I=1 TO 15:READ A$(I):NEXT I:REM READ IN STRINGS
120 LET F=0:LET I=1:REM SET EXCHANGE FLAG TO ZERO AND SUBSCRIPT TO 1
130 IF A$(I)<=A$(I+1) GOTO 180:REM DON'T EXCHANGE IF ELEMENTS IN ORDER
140 LET T$=A$(I+1):REM USE T$ TO SAVE A$(I+1)
150 LET A$(I+1)=A$(I):REM EXCHANGE TWO CONSECUTIVE ELEMENTS
160 LET A$(I)=T$
170 LET F=1:REM FLAG THAT WE EXCHANGED TWO ELEMENTS
180 LET I=I+1: IF I<15 GOTO 130
185 REM ONCE WE HAVE MADE A PASS THRU ALL ELEMENTS, CHECK
187 REM TO SEE IF WE EXCHANGED ANY.  IF NOT, DONE SORTING.
190 IF F<>0 THEN 120:REM EQUIVALENT TO IF F<>0 THEN 120
200 FOR I=1 TO 15:PRINT A$(I):NEXT I: REM PRINT SORTED LIST
210 REM STRING DATA FOLLOWS
220 DATA "APPLE","DOG","CAT","MITS","ALTAIR","RANDOM"
230 DATA "MONDAY","***ANSWER***"," FOO"
240 DATA "COMPUTER","   FOO","ELP","MILWAUKEE","SEATTLE","ALBUQUERQUE"
